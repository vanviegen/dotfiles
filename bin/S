#!/usr/bin/env fish

# Run a process as another user, using sudo. This script attempts to
# preserve the current user's environment. Usage examples
#
#    S
#		--> start a root shell
#    S myCmd myArg
#		--> run 'myCmd myArg' as root
#    S :frank
#		--> run a shell as user frank
#    S :frank myCmd myArg
#		--> run 'myCmd myArg' as user frank

# Parse out the user name
switch "$argv[1]"
	case ":*"
		set userName (string sub -s 2 $argv[1])
		set --erase argv[1]
		;;
	case '*'
		set userName "root"
		;;
end

# Set $HOME. We're preserving all other env vars.
set -x HOME (eval echo ~$userName)
if test "$HOME" = "~$userName" -o "$HOME" = "/"
	set -x HOME "/tmp/sudo-$USER-$userName"
end

if test (count $argv) = 0
	# Start an interactive fish shell
	exec sudo -E su -s $SHELL --preserve-environment $userName
else
	# Run 
	set cmd ""
	for arg in $argv
		set cmd "$cmd"(string escape -- $arg)" "
	end
	exec sudo -E su -s $SHELL --preserve-environment -c "$cmd" $userName
end
